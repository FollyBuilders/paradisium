plugins {
	id 'application'
	id 'java'
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id "com.diffplug.spotless" version "6.8.0"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(18)
		vendor = JvmVendorSpec.ADOPTIUM
	}
}

repositories {
	mavenCentral()
	maven {
		url = "https://jitpack.io"
	}
}

dependencies {
	implementation fileTree(dir: 'third-party/lxstudio-ide/lib/processing-4.0b8', include: [
		'gluegen-rt-4.0b8.jar',
		'jogl-all-4.0b8.jar',
	])

	implementation group: 'com.heronarts', name: 'lxstudio', version: '0.4.1'
	implementation group: "com.github.micycle1", name: 'processing-core-4', version: '4.0b8'


	implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.2'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.2'
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

application {
	mainClass = 'com.follybuilders.paradisium.ParadisiumApp'
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	groovyGradle {
		target '*.gradle'
		greclipse()
	}
	java {
		importOrder()
		googleJavaFormat()
		removeUnusedImports()
		licenseHeader('/* (C) $YEAR */', 'package ')
	}
}
